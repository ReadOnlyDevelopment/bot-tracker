plugins {
    id "com.jfrog.bintray" version "1.8.5"
    id 'java'
    id 'maven-publish'
    id "com.github.ben-manes.versions" version "0.38.0"
}

def versionObj = new Version(major: 0, minor: 1, revision: 0)

group = 'net.readonly.dev.tracker'
version = versionObj.toString()

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url  "https://dl.bintray.com/romvoid95/maven" 
    }
}

dependencies {
    //Code safety
    compile 'com.google.code.findbugs:jsr305:3.0.2'
}

compileJava {
    options.encoding = 'UTF-8'
}

javadoc {
    def dir = new File(System.getenv("JAVA_HOME"), "bin")
    def file = new File(dir, "javadoc")
    if(!file.isFile()) file = new File(dir, "javadoc.exe")
    if(file.isFile()) {
        executable = file.getAbsolutePath()
    }
    options.addStringOption("tag", "implNote:a:Implementation Note:")
}

jar {
    manifest {
        attributes 'Implementation-Version': version
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
	
	archives sourcesJar
	archives javadocJar
}

def pomConfig = {
	licenses {
		license {
			name "GNU Lesser General Public License version 3"
			url "https://www.gnu.org/licenses/lgpl-3.0.txt"
			distribution "repo"
		}
	}
	developers {
		developer {
			id "romvoid95"
			name "ROMVoid95"
			email "rom.void95@gmail.com"
		}
	}
	
	scm {
	   url "https://github.com/ReadOnlyDevelopment/bot-tracker"
	}
}

bintray {
	user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['Tracker']
    pkg {
        repo = 'maven'
        name = 'bot-tracker'
        licenses = ['LGPL-3.0']
        vcsUrl = 'https://github.com/ReadOnlyDevelopment/bot-tracker.git'
		labels = ['metrics']
        version {
            name = project.version
			desc = 'Bot Tracker ' + project.version.toString()
			vcsTag = project.version
			released  = new Date()
        }
    }
}

publishing {
    publications {
        Tracker(MavenPublication) {
			groupId project.group
			artifactId project.archivesBaseName
			version project.version
			from components.java
			artifact sourcesJar {
				classifier 'sources'
			}
			artifact javadocJar {
				classifier 'javadoc'
			}
			pom.withXml {
				def root = asNode()
				root.appendNode('description', 'Discord Bot Useage Tracking')
				root.appendNode('name', 'bot-tracker')
				root.appendNode('url', 'https://github.com/ReadOnlyDevelopment/bot-tracker')
				root.children().last() + pomConfig
			}
        }
    }
}

class Version {
    String major, minor, revision


    String toString() {
        "${major}.${minor}.${revision}"
    }
}